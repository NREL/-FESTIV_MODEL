=start
Q_GENTOTAL(AVAILABLE_GEN,INTERVAL)..
GEN_SCHEDULE(AVAILABLE_GEN,INTERVAL) =E= SUM(GENBLOCK(G,BLOCK)$(SAMEAS(AVAILABLE_GEN,G)),GEN_BLOCK_SCHEDULE(G,BLOCK,INTERVAL))
;
=end
=start
Q_GEN_BLOCK_LIMIT1(GENBLOCK(AVAILABLE_GEN,BLOCK),INTERVAL)$(ORD(BLOCK) EQ 1)..
GEN_BLOCK_SCHEDULE(AVAILABLE_GEN,BLOCK,INTERVAL) =L= BLOCK_CAP(AVAILABLE_GEN,BLOCK)
;
=end
=start
Q_GEN_BLOCK_LIMIT2(GENBLOCK(AVAILABLE_GEN,BLOCK),INTERVAL)$(ORD(BLOCK) GT 1)..
GEN_BLOCK_SCHEDULE(AVAILABLE_GEN,BLOCK,INTERVAL) =L= BLOCK_CAP(AVAILABLE_GEN,BLOCK) - BLOCK_CAP(AVAILABLE_GEN,BLOCK-1)
;
=end
=start
Q_GENLIMIT_HIGH(GEN,INTERVAL)..
GEN_SCHEDULE(GEN,INTERVAL) =L=    GENVALUE(GEN,"CAPACITY")*UNIT_STATUS(GEN,INTERVAL)
;
=end
=start
Q_GENLIMIT_HIGH2(GEN,INTERVAL)..
GEN_SCHEDULE(GEN,INTERVAL) + SUM(RESERVETYPE$(RESERVEVALUE(RESERVETYPE,"RESERVE_ON")EQ 1 AND (RESERVEVALUE(RESERVETYPE,"RESERVE_DIR") EQ 1 OR RESERVEVALUE(RESERVETYPE,"RESERVE_DIR") EQ 3)),GEN_RESERVE_SCHEDULE(GEN,INTERVAL,RESERVETYPE))
         =L= (UNIT_STATUS(GEN,INTERVAL)+ PUMPING(GEN,INTERVAL))*GENVALUE(GEN,"CAPACITY")
;
=end
=start
Q_GENLIMIT_LOW(MIN_GEN_GEN,INTERVAL)..
GEN_SCHEDULE(MIN_GEN_GEN,INTERVAL) =G= GENVALUE(MIN_GEN_GEN,"MIN_GEN")*(UNIT_STATUS(MIN_GEN_GEN,INTERVAL)
         - UNIT_STARTINGUP(MIN_GEN_GEN,INTERVAL) - UNIT_SHUTTINGDOWN(MIN_GEN_GEN,INTERVAL))
         + UNIT_STARTINGUP(MIN_GEN_GEN,INTERVAL)*MIN(GENVALUE(MIN_GEN_GEN,"MIN_GEN"),(UNIT_STARTUPMINGENHELP(MIN_GEN_GEN,"1")*PROD(H$(ORD(H) LE ORD(INTERVAL)),UNIT_STARTINGUP(MIN_GEN_GEN,H))+
         SUM(H$(ORD(H) LE ORD(INTERVAL) AND ORD(H) GE ORD(INTERVAL) - STARTUP_PERIOD(MIN_GEN_GEN) + 1),GENVALUE(MIN_GEN_GEN,"MIN_GEN")*UNIT_STARTINGUP(MIN_GEN_GEN,H)*(MIN(1,INTERVAL_MINUTES(H)/(60*GENVALUE(MIN_GEN_GEN,"STARTUP_TIME")))))))
;
=end
=start
Q_GENLIMIT_LOW2(MIN_GEN_GEN,INTERVAL)..
GEN_SCHEDULE(MIN_GEN_GEN,INTERVAL)
         - SUM(RESERVETYPE$(RESERVEVALUE(RESERVETYPE,"RESERVE_ON")EQ 1 AND (RESERVEVALUE(RESERVETYPE,"RESERVE_DIR") EQ 2 OR RESERVEVALUE(RESERVETYPE,"RESERVE_DIR") EQ 3)),
         GEN_RESERVE_SCHEDULE(MIN_GEN_GEN,INTERVAL,RESERVETYPE)) =G= GENVALUE(MIN_GEN_GEN,"MIN_GEN")*MAX(0,(UNIT_STATUS(MIN_GEN_GEN,INTERVAL)-UNIT_STARTINGUP(MIN_GEN_GEN,INTERVAL)-UNIT_SHUTTINGDOWN(MIN_GEN_GEN,INTERVAL)))
         + UNIT_STARTINGUP(MIN_GEN_GEN,INTERVAL)*MIN(GENVALUE(MIN_GEN_GEN,"MIN_GEN"),(UNIT_STARTUPMINGENHELP(MIN_GEN_GEN,"1")*PROD(H$(ORD(H) LE ORD(INTERVAL)),UNIT_STARTINGUP(MIN_GEN_GEN,H))+
         SUM(H$(ORD(H) LE ORD(INTERVAL) AND ORD(H) GE ORD(INTERVAL) - STARTUP_PERIOD(MIN_GEN_GEN) + 1),GENVALUE(MIN_GEN_GEN,"MIN_GEN")*UNIT_STARTINGUP(MIN_GEN_GEN,H)*(MIN(1,INTERVAL_MINUTES(H)/(60*GENVALUE(MIN_GEN_GEN,"STARTUP_TIME")))))))
         - PUMPING(MIN_GEN_GEN,INTERVAL)*STORAGEVALUE(MIN_GEN_GEN,"MAX_PUMP")
;
=end
=start
Q_RAMP_RATE_UP(RAMP_LIMITED_GEN,INTERVAL)$(ORD(INTERVAL) GT 1)..
GEN_SCHEDULE(RAMP_LIMITED_GEN,INTERVAL) - GEN_SCHEDULE(RAMP_LIMITED_GEN,INTERVAL-1)
         =L= INTERVAL_MINUTES(INTERVAL)*GENVALUE(RAMP_LIMITED_GEN,"RAMP_RATE")
         *(UNIT_STATUS(RAMP_LIMITED_GEN,INTERVAL)-UNIT_STARTINGUP(RAMP_LIMITED_GEN,INTERVAL))
         + GENVALUE(RAMP_LIMITED_GEN,"MIN_GEN")*MIN(1,(INTERVAL_MINUTES(INTERVAL)/(60*GENVALUE(RAMP_LIMITED_GEN,"STARTUP_TIME"))))*UNIT_STARTINGUP(RAMP_LIMITED_GEN,INTERVAL)
         + MAX(0,INTERVAL_MINUTES(INTERVAL) - 60*GENVALUE(RAMP_LIMITED_GEN,"STARTUP_TIME"))*GENVALUE(RAMP_LIMITED_GEN,"RAMP_RATE")*UNIT_STARTINGUP(RAMP_LIMITED_GEN,INTERVAL)
         + RAMP_UP_PENALTY(RAMP_LIMITED_GEN,INTERVAL)
;
=end
=start
Q_RAMP_RATE_DOWN(RAMP_LIMITED_GEN,INTERVAL)$(ORD(INTERVAL) GT 1)..
GEN_SCHEDULE(RAMP_LIMITED_GEN,INTERVAL) - GEN_SCHEDULE(RAMP_LIMITED_GEN,INTERVAL-1)
         =G= -1*INTERVAL_MINUTES(INTERVAL)*GENVALUE(RAMP_LIMITED_GEN,"RAMP_RATE")
         *(UNIT_STATUS(RAMP_LIMITED_GEN,INTERVAL-1)-UNIT_SHUTTINGDOWN(RAMP_LIMITED_GEN,INTERVAL))
         - (GENVALUE(RAMP_LIMITED_GEN,"MIN_GEN")*MIN(1,(INTERVAL_MINUTES(INTERVAL)/(GENVALUE(RAMP_LIMITED_GEN,"SHUTDOWN_TIME")*60))*UNIT_SHUTTINGDOWN(RAMP_LIMITED_GEN,INTERVAL))
         + MAX(0,INTERVAL_MINUTES(INTERVAL) - (GENVALUE(RAMP_LIMITED_GEN,"SHUTDOWN_TIME")*60))*GENVALUE(RAMP_LIMITED_GEN,"RAMP_RATE")*UNIT_SHUTTINGDOWN(RAMP_LIMITED_GEN,INTERVAL))
         - RAMP_DOWN_PENALTY(RAMP_LIMITED_GEN,INTERVAL)*UNIT_SHUTTINGDOWN(RAMP_LIMITED_GEN,INTERVAL)
;
=end
=start
Q_VARIABLE_FORECAST(VARIABLE_CAPACITY_RESOURCE,INTERVAL)..
GEN_SCHEDULE(VARIABLE_CAPACITY_RESOURCE,INTERVAL) =L= VG_FORECAST(INTERVAL,VARIABLE_CAPACITY_RESOURCE)*UNIT_STATUS(VARIABLE_CAPACITY_RESOURCE,INTERVAL) + GENVALUE(VARIABLE_CAPACITY_RESOURCE,"PERUNIT_COST")*GENVALUE(VARIABLE_CAPACITY_RESOURCE,"CAPACITY")
;
=end
=start
Q_PREDEFINED_INTERCHANGES(INTERFACE,INTERVAL)..
         GEN_SCHEDULE(INTERFACE,INTERVAL) =E= INTERCHANGE(INTERVAL,INTERFACE);
=end
=start
Q_RAMP_RATE_UP_BASIC(RAMP_LIMITED_GEN_NOSTARTUP,INTERVAL)$(ORD(INTERVAL) GT 1)..
GEN_SCHEDULE(RAMP_LIMITED_GEN_NOSTARTUP,INTERVAL) - GEN_SCHEDULE(RAMP_LIMITED_GEN_NOSTARTUP,INTERVAL-1)
         =L= GENVALUE(RAMP_LIMITED_GEN_NOSTARTUP,"RAMP_RATE")*INTERVAL_MINUTES(INTERVAL)*UNIT_STATUS(RAMP_LIMITED_GEN_NOSTARTUP,INTERVAL) + RAMP_UP_PENALTY(RAMP_LIMITED_GEN_NOSTARTUP,INTERVAL)
;
=end
=start
Q_RAMP_RATE_UP_BASIC0(RAMP_LIMITED_GEN_NOSTARTUP,INTERVAL)$(ORD(INTERVAL) EQ 1)..
GEN_SCHEDULE(RAMP_LIMITED_GEN_NOSTARTUP,INTERVAL) - LAST_GEN_SCHEDULE(RAMP_LIMITED_GEN_NOSTARTUP)
         =L= GENVALUE(RAMP_LIMITED_GEN_NOSTARTUP,"RAMP_RATE")*INTERVAL_MINUTES(INTERVAL)*UNIT_STATUS(RAMP_LIMITED_GEN_NOSTARTUP,INTERVAL) + RAMP_UP_PENALTY(RAMP_LIMITED_GEN_NOSTARTUP,INTERVAL)
;
=end
=start
Q_RAMP_RATE_DOWN_BASIC(RAMP_LIMITED_GEN_NOSTARTUP,INTERVAL)$(ORD(INTERVAL) GT 1)..
GEN_SCHEDULE(RAMP_LIMITED_GEN_NOSTARTUP,INTERVAL) - GEN_SCHEDULE(RAMP_LIMITED_GEN_NOSTARTUP,INTERVAL-1)
         =G= -1*GENVALUE(RAMP_LIMITED_GEN_NOSTARTUP,"RAMP_RATE")*INTERVAL_MINUTES(INTERVAL)*UNIT_STATUS(RAMP_LIMITED_GEN_NOSTARTUP,INTERVAL-1) - RAMP_DOWN_PENALTY(RAMP_LIMITED_GEN_NOSTARTUP,INTERVAL)
;
=end
=start
Q_RAMP_RATE_DOWN_BASIC0(RAMP_LIMITED_GEN_NOSTARTUP,INTERVAL)$(ORD(INTERVAL) EQ 1)..
GEN_SCHEDULE(RAMP_LIMITED_GEN_NOSTARTUP,INTERVAL) - LAST_GEN_SCHEDULE(RAMP_LIMITED_GEN_NOSTARTUP)
         =G= -1*GENVALUE(RAMP_LIMITED_GEN_NOSTARTUP,"RAMP_RATE")*INTERVAL_MINUTES(INTERVAL)*UNIT_STATUS(RAMP_LIMITED_GEN_NOSTARTUP,INTERVAL-1) - RAMP_DOWN_PENALTY(RAMP_LIMITED_GEN_NOSTARTUP,INTERVAL)
;
=end