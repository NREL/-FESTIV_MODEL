=start
*STORAGE SET DEFINITIONS
*Needs UC decisions
PUMPED_STORAGE_HYDRO(GEN) = YES$(GENVALUE(GEN,"GEN_TYPE") EQ 6); 
*Does not need UC decisions
ESR(GEN) = YES$(GENVALUE(GEN,"GEN_TYPE") EQ 8);   
*Only provides A/S
LESR(GEN) = YES$(GENVALUE(GEN,"GEN_TYPE") EQ 9);     

CSP_GEN(GEN) = YES$(GENVALUE(GEN,"GEN_TYPE") EQ 11);   

STORAGE_GEN(GEN) = PUMPED_STORAGE_HYDRO(GEN) + ESR(GEN) + LESR(GEN) + CSP_GEN(GEN);    

CONVENTIONAL_STORAGE_GEN(GEN) = STORAGE_GEN(GEN) - ESR(GEN) - LESR(GEN) - CSP_GEN(GEN);   

UNCONVENTIONAL_STORAGE_GEN(GEN) = STORAGE_GEN(GEN) - CONVENTIONAL_STORAGE_GEN(GEN);  

NON_CONVENTIONAL_STORAGE(GEN) = GEN(GEN) - CONVENTIONAL_STORAGE_GEN(GEN);   

NOT_STORAGE_GEN(GEN) = GEN(GEN) - STORAGE_GEN(GEN);

LOOP (STORAGE_GEN,
         IF(STORAGEVALUE(STORAGE_GEN,"MIN_PUMP") EQ 0 OR STORAGEVALUE(STORAGE_GEN,"PUMP_STARTUP_TIME") EQ 0 OR STORAGEVALUE(STORAGE_GEN,"PUMP_SHUTDOWN_TIME") EQ 0,
                 IF(STORAGEVALUE(STORAGE_GEN,"PUMP_RAMP_RATE") GT 0 AND STORAGEVALUE(STORAGE_GEN,"PUMP_RAMP_RATE") LT STORAGEVALUE(STORAGE_GEN,"MAX_PUMP")/(INTERVAL_LENGTH*60),
                         RAMP_LIMITED_CONVENTIONAL_STORAGE_GEN_NOSTARTUP(STORAGE_GEN) = YES;
                   );
         ELSE
                 IF((STORAGEVALUE(STORAGE_GEN,"PUMP_RAMP_RATE") GT 0 AND  (STORAGEVALUE(STORAGE_GEN,"MAX_PUMP") - STORAGEVALUE(STORAGE_GEN,"MIN_PUMP"))/STORAGEVALUE(STORAGE_GEN,"PUMP_RAMP_RATE") + 60*MAX(STORAGEVALUE(STORAGE_GEN,"PUMP_STARTUP_TIME"),STORAGEVALUE(STORAGE_GEN,"PUMP_SHUTDOWN_TIME")) GT (INTERVAL_LENGTH*60)),
                         RAMP_LIMITED_CONVENTIONAL_STORAGE_GEN(STORAGE_GEN) = YES;
                   );
            );
     );
RAMP_LIMITED_CONVENTIONAL_STORAGE_GEN(CONVENTIONAL_STORAGE_GEN) = RAMP_LIMITED_CONVENTIONAL_STORAGE_GEN(CONVENTIONAL_STORAGE_GEN) - OUTAGE_GEN(CONVENTIONAL_STORAGE_GEN);

MIN_GEN_CONVENTIONAL_STORAGE_GEN(CONVENTIONAL_STORAGE_GEN) = YES$(STORAGEVALUE(CONVENTIONAL_STORAGE_GEN,"MIN_PUMP") NE 0);
MIN_GEN_CONVENTIONAL_STORAGE_GEN(CONVENTIONAL_STORAGE_GEN) = MIN_GEN_CONVENTIONAL_STORAGE_GEN(CONVENTIONAL_STORAGE_GEN) - OUTAGE_GEN(CONVENTIONAL_STORAGE_GEN);
=end